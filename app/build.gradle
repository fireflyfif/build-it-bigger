apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Get the Local IP Address of the machine for testing the backend server on a real devices
        // resource: http://disq.us/p/1dqd3fz
        buildConfigField "String", "LOCAL_API", String.format('"http://%s:8080/_ah/api/"', InetAddress.getLocalHost().getCanonicalHostName())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "mode"

    productFlavors {
        free {
            dimension "mode"
            applicationIdSuffix ".free"
        }

        paid {
            dimension "mode"
            applicationIdSuffix ".paid"
        }
    }
}

configurations {
    all {
        exclude module: 'httpclient'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.google.android.gms:play-services-ads:$rootProject.playServiceVersion"
    implementation "com.google.api-client:google-api-client:$rootProject.googleClientVersion"
    implementation "com.google.http-client:google-http-client-android:$rootProject.googleClientVersion"
    implementation "com.google.code.findbugs:jsr305:$rootProject.findBugsVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    // Add explicitly this library so that there are no conflicts with older versions
    // source: https://stackoverflow.com/a/42374426/8132331
    implementation "com.android.support:customtabs:$rootProject.supportLibraryVersion"

    // Dependency to the Joke supplying Java library
    implementation project(':joke')

    // Dependency to the Joke displaying Android library
    implementation project(':joker')

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoCoreVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "com.android.support.test:rules:$rootProject.espressoRulesVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espressoIntentsVersion"

    testImplementation "junit:junit:$rootProject.jUnitTestVersion"
}
